; Copies the memory image of CP/M onto tracks 0 and 1 of 
; the first CP/M disk
;
; Load and run from ROM monitor
; Uses BIOS functions at page 0F6xxh
; Writes track 0, sectors 2 to 26, then track 1, sectors 1 to 25
; (24+25)*128 bytes = 1880h 
;
CCP	equ	0E000h		; Start of CP/M code in memory

boot	equ 	CCP+1600h	; Boot
wboot	equ	CCP+1603h  	; Warmboot
seldsk	equ 	CCP+161bh  	; pass disk no. in c
setdma	equ 	CCP+1624h 	; pass address in bc
settrk	equ 	CCP+161eh 	; pass track in reg C
setsec	equ	CCP+1621h 	; pass sector in reg c
write	equ 	CCP+162ah 	; write one CP/M sector to disk


	org 	0100h

	ld 	c,00h 	;CP/M disk a
	call 	seldsk
;Write track 0, sectors 2 to 26
	ld 	a,2 	;starting sector
	ld 	(sector),a
	ld 	hl,CCP
	ld 	(address),hl
	ld 	c,0 	;CP/M track
	call  	settrk
wr_trk_0_loop:  	ld 	a,(sector)
	ld 	c,a 	;CP/M sector
	call 	setsec
	ld 	bc,(address) 	;memory location
	call 	setdma
	call 	write
	ld 	a,(sector)
	cp 	26 	;done:
	jp 	z,wr_trk_1 	;yes, start writing track 1
	inc 	a 	;no, next sector
	ld 	(sector),a
	ld 	hl,(address)
	ld 	de,128
	add 	hl,de
	ld 	(address),hl
	jp 	wr_trk_0_loop
;Write track 1, sectors 1 to 25
wr_trk_1: 	ld 	c,1
	call  	settrk
	ld 	hl,(address)
	ld 	de,128
	add 	hl,de
	ld 	(address),hl
	ld 	a,1
	ld 	(sector),a
wr_trk_1_loop:  	ld 	a,(sector)
	ld 	c,a 	;CP/M sector
	call 	setsec
	ld 	bc,(address)	;memory location
	call 	setdma
	call 	write
	ld 	a,(sector)
	cp 	25
	jp 	z,done
	inc 	a
	ld 	(sector),a
	ld 	hl,(address)
	ld 	de,128
	add 	hl,de
	ld 	(address),hl
	jp 	wr_trk_1_loop
done: 	jp 	boot

sector: 	db 	00h
address: 	dw 	0000h

	end
